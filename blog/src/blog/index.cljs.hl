(page "index.html"
  (:require
    [blog.db :as db]
    [blog.markdown-editor :as md-e]
    [blog.route :as r]
    [blog.vega :as b-v]
    [cljsjs.highlight.langs.clojure]
    [clojure.string :as str]
    [datascript :as d]
    [hoplon.highlight :as hl]
    [hoplon.markdown :as md]
    [hoplon.mui :as m]
    [hoplon.vega :as vega]))

(defn timestamps [created-at updated-at]
  (span :css {:color "grey"}
    (str "Original date: " created-at (when-let [update (first updated-at)] (str " / Last updated: " update)))))

(defn render [[tag s]]
  (case tag
    :md (md/to-html div s)
    :code (hl/highlight s)))

(defn post [{:keys [title content created-at updated-at] :as m}]
  [(h1 title)
   (timestamps created-at updated-at)
   (m/panel
     (map render content))])

(defn post-link [[title date permalink]]
  (div :class "mui-text-title"
    (a :href (str "#/blog/" permalink)
      (str date ": " title))))

(defn list-posts [conn]
  [(h1 "Posts:")
   (m/panel
     (->> (db/post-list conn)
          (sort-by second >)
          (map post-link)))])

(html
  (head
    (link :href "blog.css" :rel "stylesheet"))
  (body
    (m/appbar
      (td
        (a
         :class "mui-text-headline mui-text-accent"
         :href "#"
         "mynomoto's experiments")))
    (div
      :class "mui-container"
      :css {:display "none"}
      :toggle (cell= (= r/main-hash "chart"))
      (h1
        :click #(swap! b-v/dataset (fn [data]
                                     (mapv (fn [d]
                                             (update-in d [:y] inc))
                                       data)))
        "Sample chart")
      (input
        :css {:width "400px"}
        :type "range" :min 1 :max 1101 :step 5
        :value b-v/width
        :input #(reset! b-v/width @%))
      (input
        :css {:width "400px"}
        :type "range" :min 1 :max 401 :step 5
        :value b-v/height
        :input #(reset! b-v/height @%))
      (vega/chart b-v/json-spec))
    (md-e/live-editor
      (cell (or (db/retrieve "mynomoto/md-editor")
                md-e/sample-content))
      r/main-hash)

    (div
      :class "mui-container"
      :css {:display "none"}
      :toggle (cell= (= r/main-hash "blog"))
      (cell= (if (str/blank? r/permalink)
               (list-posts db/conn)
               (post (db/current-post r/permalink db/conn)))))))
